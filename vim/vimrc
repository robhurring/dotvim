" vim: fdm=marker

let s:local_plugins = expand('~/.vim-plugins.local')
let s:local_rc = expand('~/.vimrc.local')

let g:mapleader="\<Space>"
let g:maplocalleader=','
inoremap jk <Esc>l

filetype plugin indent on
syntax on

" Plugins {{{
call plug#begin('~/.vim/plugged')

" dependencies
Plug 'tmhedberg/matchit'
Plug 'tpope/vim-dispatch'    " dep for: vim-test(O), vim-go(O)

" misc plugins
Plug 'tommcdo/vim-lion'
let b:lion_squeeze_spaces = 1

Plug 'vim-scripts/ReplaceWithRegister'
Plug 'majutsushi/tagbar', {'on': 'TagbarToggle'}
Plug 'sbdchd/neoformat'
Plug 'skywind3000/asyncrun.vim'
Plug 'tpope/vim-commentary'
Plug 'tpope/vim-fugitive' " git mergetool
Plug 'tpope/vim-rhubarb' " git mergetool
Plug 'tpope/vim-endwise'
Plug 'tpope/vim-repeat'
Plug 'tpope/vim-surround'
Plug 'tpope/vim-unimpaired'
Plug 'tpope/vim-projectionist'
Plug 'benmills/vimux'                   " dep for: vim-test
" SEE: https://github.com/mhinz/dotfiles/blob/master/vim/vimrc#L349-L359
Plug 'jszakmeister/vim-togglecursor'    " only needed for vim/mvim
let g:togglecursor_force = 'xterm'

Plug 'AndrewRadev/splitjoin.vim'
Plug 'itchyny/lightline.vim'
Plug 'Konfekt/FastFold'
let g:fastfold_savehook = 0

Plug 'junegunn/vim-peekaboo'
let g:peekaboo_delay = 0
let g:peekaboo_compact = 1
let g:peekaboo_window = "bel bo 10new"

" finder
Plug 'liuchengxu/vista.vim'
let g:vista_close_on_jump = 1

Plug 'liuchengxu/vim-clap', { 'do': ':Clap install-binary!' }
Plug 'haya14busa/incsearch.vim'
let g:incsearch#auto_nohlsearch = 1
let g:incsearch#magic = '\v'

Plug 'mhinz/vim-grepper'
let g:grepper = {
      \'open': 1,
      \'jump': 0,
      \'tools': ['rg', 'ag', 'lag', 'git', 'grep', 'findstr'],
      \ 'lag': {
      \   'grepprg': 'ag --vimgrep $* $.',
      \   'grepformat': '%f:%l:%c:%m,%f:%l:%m',
      \   'escape': '\^$.*+?()[]%#'
      \ }
      \}

Plug 'SirVer/ultisnips'
let g:UltiSnipsExpandTrigger      = '<Tab>'
let g:UltiSnipsSnippetDirectories = [$HOME.'/.vim/UltiSnips', $HOME.'/.vim/snippets']
let g:UltiSnipsListSnippets       = '<C-l>'

Plug 'scrooloose/nerdtree'
let g:NERDTreeIgnore=['\.pyc$', '\.pyo$', '\.py\$class$', '\.obj$', '\.o$', '\.so$', '^\.git$']
let g:NERDTreeQuitOnOpen = 1
let g:NERDTreeMinimalUI = 1

Plug 'w0rp/ale'
let g:ale_lint_on_text_changed = 'never'
let g:ale_lint_on_enter = 0
let g:ale_lint_on_save = 0
let g:ale_sign_error = "\u25CF"
let g:ale_sign_warning = "\u25CF"
let g:ale_linters = {
      \   'javascript': [],
      \   'ruby': []
      \}

" Plug 'ludovicchabant/vim-gutentags'
" let g:gutentags_define_advanced_commands = 1
" let g:gutentags_cache_dir = '~/.cache/tags'

" NOTE: this fails from the res/ctags.options file since ripper-tags
"       doesn't allow the --options flag
" SEE: https://github.com/ludovicchabant/vim-gutentags/issues/56
if executable('ripper-tags')
  let g:gutentags_ctags_executable_ruby = 'ripper-tags'
endif

Plug 'jiangmiao/auto-pairs'
let g:AutoPairsMapSpace = 0
let g:AutoPairsMultilineClose = 0
let g:AutoPairsShortcutBackInsert = '<M-b>'
let g:AutoPairsShortcutFastWrap = '<M-e>'
let g:AutoPairsShortcutJump = '<M-n>'

" languages
Plug 'sheerun/vim-polyglot'
let g:vim_markdown_conceal = 0
let g:vim_markdown_conceal_code_blocks = 0
let g:jsx_ext_required = 0

" markdown/etc
let g:vim_markdown_fenced_languages = ['javascript', 'ruby', 'sh', 'yaml', 'html', 'vim', 'json']
Plug 'davinche/godown-vim', {'for': 'markdown'}

" html/css/js
Plug 'tpope/vim-jdaddy', {'for': ['json', 'javascript']}
Plug 'tpope/vim-ragtag'
Plug 'mattn/emmet-vim', {'for': ['javascript', 'html']}
let g:user_emmet_leader_key='<C-e>'

" go
Plug 'fatih/vim-go', {'for': 'go'}
let g:go_highlight_functions = 1
let g:go_highlight_methods = 1
let g:go_highlight_structs = 1
let g:go_highlight_operators = 1
let g:go_highlight_build_constraints = 1
let g:go_dispatch_enabled = 1
let g:go_fmt_experimental = 0
let g:go_fmt_autosave = 1
let g:go_fmt_command = 'goimports'

" ruby vim-ruby from polyglot
Plug 'tpope/vim-bundler'  " used for appending gem tags to `tagfiles()`
Plug 'tpope/vim-cucumber'
Plug 'janko-m/vim-test'
let g:test#filename_modifier = ':p'
let g:test#strategy = 'vimux'
let g:test#ruby#rspec#executable = 'auto-bundle-exec rspec --require=~/.vim/config/rspec_vim_formatter.rb --format VimFormatter --out ~/.vim/tmp/quickfix.out --format progress'
let g:test#ruby#cucumber#executable = 'auto-bundle-exec cucumber --backtrace -r features'

" autocomplete
Plug 'neoclide/coc.nvim', {'branch': 'release'}

" rust
Plug 'rust-lang/rust.vim', {'for': 'rust'}

" typescript
Plug 'Quramy/tsuquyomi', {'for': 'typescript'}

" local bundles
if filereadable(s:local_plugins)
  exec 'source ' . s:local_plugins
endif
call plug#end()
" }}} /plugins

" Options {{{
" NOTE: some are unnecessary in neovim, but needed for vim compatibility
" http://vimdoc.sourceforge.net/htmldoc/quickref.html

set t_ut= " see: http://sunaku.github.io/vim-256color-bce.html

set autoindent
set autoread
set backspace=indent,eol,start
set backupdir=~/.vim/tmp
set breakat=\ ^I*-+;,/?\(\[\{ " fix syntax
set breakindent
set breakindentopt=shift:2
set clipboard=unnamed
set colorcolumn=81
set completeopt-=preview
set concealcursor=nc
set conceallevel=2
set cursorline
set diffopt=filler,iwhite,vertical
set directory=~/.vim/swap
set expandtab
set foldlevel=1
set foldmethod=indent
set foldnestmax=10
"set guifont=Hasklig-Regular:h15
set guioptions-=r
set hidden
set hlsearch
set ignorecase
set incsearch
set laststatus=2
set lazyredraw
set linebreak
set list
set listchars=tab:-\ ,nbsp:∙,extends:»,precedes:«
set mouse=a
set nofoldenable
set norelativenumber
set nowrap
set number
set scrolloff=3
set sessionoptions-=help
set shiftround
set shiftwidth=2
set shortmess+=A
set showbreak=↪\
set showmatch
set sidescroll=1
set sidescrolloff=3
set smartcase
set smartindent
set smarttab
set splitbelow
set splitright
set tabstop=2
set tags+=./tags,./git/tags
set undodir=~/.vim/undo
set undofile
set updatetime=750
set virtualedit=block
set wildignore+=*.pyc,*.class,*.sln,*.Master,*.csproj,*.csproj.user,*.cache,*.dll,*.pdb,*.min.*
set wildignore+=*.tar.*
set wildignore+=*/.git/**/*,*/.hg/**/*,*/.svn/**/*
set wildignore+=*/min/*,*/vendor/*,*/node_modules/*,*/bower_components/*
set wildignore+=*/tmp/*,*/log/*,*.zip,*.so,*.swp,*.bak,*/undo/*,*/swap/*
set wildignore+=tags,cscope.*
set wildmenu

" neovim specific options
if has('nvim')
  set inccommand=split
end

" SEE: https://gist.github.com/XVilka/8346728
" NOTE: not working with vim/tmux currently
if has('termguicolors')
  set termguicolors
end

if has('gui_running')
  set ballooneval
  set macligatures
endif

" use zsh on where available
" NOTE: causes considerable lag
" if executable('zsh')
"   set shell=zsh
" endif
" set shellcmdflag=-lc

if executable('ag')
  set grepprg=ag\ -f\ --vimgrep\ $*
  set grepformat=%f:%l:%c:%m
endif

" SEE: https://github.com/BurntSushi/ripgrep
if executable('rg')
  set grepprg=rg\ -L\ --vimgrep\ $*
  set grepformat=%f:%l:%c:%m
endif

if executable('prettier')
  let g:neoformat_enabled_javascript = ['prettier']
  let g:ale_linters['javascript'] += ['prettier']
endif

if executable('shfmt')
  let g:neoformat_enabled_sh = ['shfmt']
endif

if executable('rubocop')
  let g:ale_linters['ruby'] += ['rubocop']
  let g:neoformat_enabled_ruby = ['rubocop']
endif

if executable('reek')
  let g:ale_linters['ruby'] += ['reek']
endif

if has('nvim')
  let $NVIM_TUI_ENABLE_TRUE_COLOR=1
  let $NVIM_TUI_ENABLE_CURSOR_SHAPE=1
  set viminfo='100,/100,h,\"500,:100
else
  set viminfo='100,/100,h,\"500,:100,n~/.vim/viminfo
endif

" netrw tree view
let g:netrw_liststyle = 3

" }}}

" Theme"{{{
try
  execute 'set colorcolumn=' . join(range(81,200), ',')
  set background=dark
  let g:jellybeans_overrides = {
        \ 'ColorColumn': { 'guibg': '191919', 'ctermbg': 'none' },
        \ 'SignColumn': { 'guibg': '151515', 'ctermbg': 'none' },
        \ 'CursorLine': { 'guibg': '1c1c1c', 'ctermbg': 'none' }
        \ }
  colorscheme jellybeans
catch
  echom 'Missing color scheme!'
endtry

" syntax debugging
" map <F12> :echo "hi<" . synIDattr(synID(line("."),col("."),1),"name") . '> trans<'
"       \ . synIDattr(synID(line("."),col("."),0),"name") . "> lo<"
"       \ . synIDattr(synIDtrans(synID(line("."),col("."),1)),"name") . ">"<CR>

" Theme overrides
" http://www.colorpicker.com

" linter highlightings
highlight ALEErrorSign ctermfg=205 guifg=#e5786d
highlight link ALEWarningSign LineNr

" highlight trailing white space (via conceal)
highlight ExtraWhitespace   ctermfg=196 ctermbg=197 guibg=NONE guifg=#FF0080
highlight Conceal           ctermfg=196 ctermbg=197 guibg=NONE guifg=#FF0080
"}}}

" Mappings {{{

" make Y behave like D
nnoremap Y y$

" autocomplete / intellisense
nmap <silent> gd <Plug>(coc-definition)
nmap <silent> gy <Plug>(coc-type-definition)
nmap <silent> gi <Plug>(coc-implementation)
nmap <silent> gr <Plug>(coc-references)
nmap <leader>rn <Plug>(coc-rename)
nmap <leader>qf  <Plug>(coc-fix-current)
inoremap <silent><expr> <C-j> pumvisible() ? "\<C-n>" : coc#refresh()
inoremap <expr><C-k> pumvisible() ? "\<C-p>" : "\<C-k>"
inoremap <silent><expr> <C-Space> coc#refresh()
nnoremap <silent> K :call <SID>show_documentation()<CR>

function! s:show_documentation()
  if (index(['vim','help'], &filetype) >= 0)
    execute 'h '.expand('<cword>')
  else
    call CocAction('doHover')
  endif
endfunction

augroup CocGroup
  autocmd!
  autocmd CursorHold * silent call CocActionAsync('highlight')
  autocmd User CocJumpPlaceholder call CocActionAsync('showSignatureHelp')
augroup END

" Map function and class text objects
xmap if <Plug>(coc-funcobj-i)
omap if <Plug>(coc-funcobj-i)
xmap af <Plug>(coc-funcobj-a)
omap af <Plug>(coc-funcobj-a)
xmap ic <Plug>(coc-classobj-i)
omap ic <Plug>(coc-classobj-i)
xmap ac <Plug>(coc-classobj-a)
omap ac <Plug>(coc-classobj-a)

" horizontal moves
nmap <Right> 10zl
nmap <Left> 10zh

" yank current file + line
nnoremap <silent> <F5> :let @*=expand("%").":".line(".")\|echom "Copied"<CR>

" don't move cursor for *
nnoremap * *``

" buffers / windows
nnoremap <silent> <C-x> :bunload<CR>
nnoremap <silent> <C-q> :bdelete<CR>
nnoremap <leader><leader> :Clap buffers<CR>
nnoremap <localleader><localleader> :b#<CR>

" fuzzy tags
nnoremap <C-t> :Clap tags<CR>

" up/down on displayed lines, not real lines. (except when using counts)
noremap gk k
noremap gj j
noremap <expr> k (v:count > 1 ? "m'" . v:count . 'k': 'gk')
noremap <expr> j (v:count > 1 ? "m'" . v:count . 'j': 'gj')

" toggle folds
nnoremap <silent> \ @=(foldlevel('.')?'za':"\\")<CR>
vnoremap \ zf

" finder mappings
nnoremap <Plug>QuickOpen :NERDTreeClose\|:Clap files<cr>
" nmap <NUL> <Plug>QuickOpen
" nmap <C-t> <Plug>QuickOpen
nmap <C-Space> <Plug>QuickOpen
nmap <C-@> <C-Space>

" toggle quickfix/location
" SEE: plugins/toggle-quickfix.vim
nmap <leader>l <Plug>(toggle-only-location-list)
nmap <leader>c <Plug>(toggle-only-quickfix-list)

" cd to buffer's project root directory
" SEE: autoload/helpers.vim
nmap <silent> <localleader>cd :execute 'cd '.helpers#projectroot()<CR>

" saving (keep imap to avoid vim-surround from binding it)
inoremap <C-s> <Esc>:update<CR>
nnoremap <C-s> <Esc>:update<CR>
cmap w!! w !sudo tee > /dev/null %

" splits and such
nnoremap <C-j> <C-w>j
nnoremap <C-k> <C-w>k
nnoremap <C-l> <C-w>l
nnoremap <C-h> <C-w>h

" fix nvim's <C-h>
" SEE: https://github.com/neovim/neovim/issues/2048#issuecomment-78045837
" infocmp $TERM | sed 's/kbs=^[hH]/kbs=\\177/' > $TERM.ti tic $TERM.ti
if has('nvim')
  " nmap <BS> <C-h>
endif

" repeatng
vnoremap . :normal .<CR>
vnoremap @ :normal! @

" search/replace settings
nnoremap <silent> <leader>/ :noh<CR>

nnoremap <localleader>/ :%s/\<<c-r><c-w>\>//Ic<left><left><left>
vnoremap <localleader>/ "zy:%s/<C-r>z//gc<left><left><left>
vnoremap // "zy/<C-r>=escape(@z,'/\.*$^~[]()')<CR><CR>N
nnoremap // /<C-r>/

" search/replace similar to sublime (use `.` to quickly repeat, `n` to skip)
" Credit: http://www.kevinli.co/posts/2017-01-19-multiple-cursors-in-500-bytes-of-vimscript
let g:mc = "y/\\V\<C-r>=escape(@\", '/')\<CR>\<CR>"

nnoremap cn *``cgn
nnoremap cN *``cgN
vnoremap <expr> cn g:mc . "``cgn"
vnoremap <expr> cN g:mc . "``cgN"

" search/replace macros
function! SetupCR()
  nnoremap <Enter> :nnoremap <lt>Enter> n@z<CR>q:<C-u>let @z=strpart(@z,0,strlen(@z)-1)<CR>n@z
endfunction

nnoremap cq :call SetupCR()<CR>*``qz
nnoremap cQ :call SetupCR()<CR>#``qz
vnoremap <expr> cq ":\<C-u>call SetupCR()\<CR>" . "gv" . g:mc . "``qz"
vnoremap <expr> cQ ":\<C-u>call SetupCR()\<CR>" . "gv" . substitute(g:mc, '/', '?', 'g') . "``qz"

" searching
command! -nargs=* -complete=file Ag Grepper -tool ag -query <args>

if executable('rg')
  command! -nargs=* -complete=file Ag Grepper -tool rg -query <args>
endif

" operator pending: grep operators
nmap gs  <plug>(GrepperOperator)
xmap gs  <plug>(GrepperOperator)

" incsearch.vim
" nnoremap <Esc><Esc> :<C-u>nohlsearch<CR>
map / <Plug>(incsearch-forward)
map ?  <Plug>(incsearch-backward)
map g/ <Plug>(incsearch-stay)
map n  <Plug>(incsearch-nohl-n)
map N  <Plug>(incsearch-nohl-N)

" re-select pasted text
noremap gv `[v`]

" don't move cursor when joining
nnoremap J mzJ`z

" yanks
xnoremap <leader>p "0p
xnoremap <leader>P "0P
nmap <Leader>y :Clap yanks<CR>

" vim-test
nnoremap <silent> <Leader>t :TestFile<CR>
nnoremap <silent> <Leader>T :TestNearest<CR>
nnoremap <silent> <Leader>tl :TestLast<CR>
nnoremap <silent> <Leader>ta :TestSuite<CR>

" load vim-test failures into quickfix
nnoremap <silent> <leader>sc :cg ~/.vim/tmp/quickfix.out\|copen<CR>

" splitjoin
nnoremap gS :SplitjoinSplit<cr>
nnoremap gJ :SplitjoinJoin<cr>

" vim-commentary <C-/>
map <C-_> gcc<Esc>

" vim-unimpaired / line bubbling
imap <C-Up> <Esc>[egi
imap <C-Down> <Esc>]egi
nmap <C-Up> [e
nmap <C-down> ]e
vmap <C-Up> [egv
vmap <C-Down> ]egv

" neoformat / fix-whitespace
nmap <silent> <leader>= :Neoformat<CR>
" nmap <silent> <F1> :RemoveTrailingSpaces<CR>

" nerdtree
nnoremap <leader>e :NERDTreeToggle<cr>
nnoremap <leader>E :NERDTreeFind<cr>

" tagbar
nnoremap <localleader>t :Vista!!<cr>

" Tab complete
" imap <Tab> <C-P>

" }}}1 /Mappings

" Augroups {{{

augroup VimrcGroup
  autocmd!

  " pickup where you left off
  autocmd BufReadPost * if &filetype !~ '^git\c' && line("'\"") > 0 && line("'\"") <= line("$") | exe "normal! g`\"" | endif

  " kick-off conceal extra whitespace
  autocmd BufRead,InsertLeave * syntax match ExtraWhitespace '\s\+$' containedin=ALL conceal cchar=∙
  autocmd BufReadPost * setlocal conceallevel=2 " some plugins are changing this so no visible whitespace

  " " odd file extensions
  autocmd BufNewFile,BufRead {*.ejs}    set filetype=html
  autocmd BufNewFile,BufRead {*.js.erb} set filetype=javascript
  autocmd BufRead,BufNewFile {Procfile} set filetype=yaml

  " <C-X>= for #{}
  autocmd FileType yaml,eruby inoremap <buffer> <C-X>= <%=  %><Esc>hhi
  autocmd FileType ruby       inoremap <buffer> <C-X>= #{}<Esc>i
  autocmd FileType ruby,eruby setlocal iskeyword+=?,!

  " whitespace language tweaks
  autocmd FileType python,go setlocal nolist tabstop=2 shiftwidth=2 softtabstop=2 noexpandtab

  " wrap markdown
  autocmd FileType markdown setlocal wrap

  " auto-reload vimrc
  autocmd BufWritePost {.vimrc,vimrc,.vimrc.local} nested source %

  " fix awful red colorcolumns in certain themes
  " autocmd ColorScheme * highlight ColorColumn guibg=#222222 ctermbg=235

  " linters -- set b:skip_linters to run manually
  " NOTE: can be used with `autocmd` to disable linting for specific paths
  "       (put in .vimrc.local)
  autocmd BufWritePost * if !exists('b:skip_linters') | ALELint | endif

  " auto-mark some common files
  autocmd BufLeave {.vimrc,vimrc,init.vim} normal mV
  autocmd BufLeave .zshrc normal mZ
  autocmd BufLeave .zshenv normal mE
  autocmd BufLeave {.ideavimrc,ideavimrc} normal mI

  let dontMark = ['nerdtree']
  autocmd BufLeave * if index(dontMark, &ft) < 0 | normal mL

  autocmd FileType terraform setlocal commentstring=#\ %s
augroup END

" }}} /augroups

if filereadable(s:local_rc)
  exec 'source ' . s:local_rc
endif

